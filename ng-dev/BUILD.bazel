load("//tools:defaults.bzl", "esbuild", "ts_library")

NG_DEV_EXTERNALS = [
    # `ts-node` is optional for users who write their configurations in TypeScript.
    "ts-node",
    # `typescript` is external because we want the project to provide a TypeScript version.
    # TODO: Figure out how we want to manage dependencies for the dev-infra tool.
    "typescript",
]

ts_library(
    name = "ng-dev",
    srcs = [
        "cli.ts",
    ],
    visibility = [
        "//:npm",
        "//ng-dev:__subpackages__",
    ],
    deps = [
        "//ng-dev/caretaker",
        "//ng-dev/ci",
        "//ng-dev/commit-message",
        "//ng-dev/format",
        "//ng-dev/misc",
        "//ng-dev/ngbot",
        "//ng-dev/pr",
        "//ng-dev/pullapprove",
        "//ng-dev/release",
        "//ng-dev/ts-circular-dependencies",
        "//ng-dev/utils",
        "@npm//@types/yargs",
        "@npm//yargs",
    ],
)

esbuild(
    name = "cli-bundle",
    entry_point = "cli.ts",
    external = NG_DEV_EXTERNALS,
    output = "cli-bundle.js",
    deps = [
        ":ng-dev",
    ],
)

esbuild(
    name = "build-worker-bundle",
    entry_point = "//ng-dev/release/build:build-worker.ts",
    external = NG_DEV_EXTERNALS,
    # Note: Needs to be named `build-worker.js` as this is the external path
    # the release build command resolves the script from.
    output = "build-worker.js",
    deps = [
        "//ng-dev/release/build",
    ],
)

genrule(
    name = "commit-message-filter",
    srcs = ["//ng-dev/pr/merge:strategies/commit-message-filter.js"],
    # Note: Needs to be named `commit-message-filter.js` as this is the name
    # the autosquash merge strategy expects the external file to be named.
    outs = ["commit-message-filter.js"],
    cmd = """cp $< $@""",
)

filegroup(
    name = "lib",
    srcs = [
        ":cli-bundle",
        # The bundle of `ng-dev` relies on the following files to be available next to the `ng-dev`
        # bundle. This is necessary because the CLI resolves these files using relative paths and
        # requires these to be external, compared to bundling those up.
        ":build-worker-bundle",
        ":commit-message-filter",
    ],
    visibility = ["//:npm"],
)
