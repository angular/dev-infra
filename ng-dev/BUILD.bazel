load("@devinfra_npm//:defs.bzl", "npm_link_all_packages")
load("//:package.bzl", "NPM_PACKAGE_SUBSTITUTIONS")
load("//tools:defaults.bzl", "esbuild", "extract_types", "npm_package", "ts_config", "ts_project")

npm_link_all_packages()

ts_config(
    name = "tsconfig",
    src = "tsconfig.json",
    visibility = [
        "//ng-dev:__subpackages__",
    ],
    deps = [
        "//ng-dev:node_modules/@types/node",
    ],
)

ts_config(
    name = "tsconfig_test",
    src = "tsconfig-test.json",
    visibility = [
        "//ng-dev:__subpackages__",
    ],
    deps = [
        ":tsconfig",
    ],
)

NG_DEV_EXTERNALS = [
    # `typescript` is external because we want the project to provide a TypeScript version.
    # TODO: Figure out how we want to manage dependencies for the dev-infra tool.
    "typescript",
    # Packages using `__filename` and `__dirname` and cannot be bundled in ESM. We do not
    # intend to provide interop globals for this as it could hide other significant issues.
    "@yarnpkg/lockfile",
    "@google-cloud/spanner",
    "tsx",
]

ts_project(
    name = "ng-dev",
    srcs = [
        "cli.ts",
        "index.ts",
    ],
    visibility = [
        "//ng-dev:__subpackages__",
    ],
    deps = [
        "//ng-dev:node_modules/@types/yargs",
        "//ng-dev:node_modules/yaml",
        "//ng-dev:node_modules/yargs",
        "//ng-dev/ai",
        "//ng-dev/auth",
        "//ng-dev/caretaker",
        "//ng-dev/commit-message",
        "//ng-dev/config",
        "//ng-dev/format",
        "//ng-dev/misc",
        "//ng-dev/ngbot",
        "//ng-dev/perf",
        "//ng-dev/pr",
        "//ng-dev/pr/common/labels",
        "//ng-dev/pr/config",
        "//ng-dev/pullapprove",
        "//ng-dev/release",
        "//ng-dev/release/config",
        "//ng-dev/release/precheck",
        "//ng-dev/release/publish",
        "//ng-dev/release/stamping",
        "//ng-dev/release/versioning",
        "//ng-dev/ts-circular-dependencies",
        "//ng-dev/utils",
    ],
)

extract_types(
    name = "types",
    deps = [":ng-dev"],
)

esbuild(
    name = "bundles",
    config = {
        "resolveExtensions": [
            ".mjs",
            ".js",
            ".json",
        ],
        "outExtension": {".js": ".mjs"},
        "banner": {
            "js": """
import {createRequire as __cjsCompatRequire} from 'module';
const require = __cjsCompatRequire(import.meta.url);
""",
        },
        "mainFields": [
            "module",
            "main",
        ],
    },
    entry_points = [
        "cli.js",
        "index.js",
        # These additional entry-points need to be generated since the `ng-dev` tool tries
        # to launch these files/scripts dynamically (through e.g. `spawn` or `fork`).
        "//ng-dev/release/build:build-worker",
        "//ng-dev/pr/merge:commit-message-filter",
        "//ng-dev/pr/checkout:commit-message-filter",
    ],
    external = NG_DEV_EXTERNALS,
    format = "esm",
    platform = "node",
    sourcemap = "external",
    splitting = True,
    tags = [
        # We prevent this from executing remotely so that the `EXTERNAL` packages are still present in
        # node_modules when running `ng-dev` locally.
        "no-remote",
    ],
    target = "node22",
    deps = [
        ":ng-dev",
    ],
)

npm_package(
    name = "npm_package",
    srcs = [
        "package.json",
        ":bundles",
        ":types",
    ],
    package = "@angular/ng-dev",
    substitutions = NPM_PACKAGE_SUBSTITUTIONS,
)
